group 'com.tesults.testng'
version '1.0.3'

apply plugin : "java"
apply plugin : "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'org.testng:testng:6.10'
    implementation 'com.tesults:tesults:1.0.2'
    //implementation fileTree(dir: 'temp', includes: ['*.jar'])
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'tesults-testng'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'tesults-testng'
                description = 'Tesults TestNG Listener'
                url = 'https://www.tesults.com'
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "ajeetd"
                        name = "Ajeet Dhaliwal"
                        email = "help@tesults.com"
                    }
                }

                scm {
                    url = "https://github.com/tesults/testng.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}